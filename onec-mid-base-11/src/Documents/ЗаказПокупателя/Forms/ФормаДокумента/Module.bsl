
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// !!!
	// Неборак И.А.:Вывод реквизита КонтактноеЛицо на форму
	ПолеВвода = Элементы.Добавить("НИ_КонтактноеЛицо", Тип("ПолеФормы"),Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.НИ_КонтактноеЛицо"; 
	
	// Добавление группы Скидка
	Группа = Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"),Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
	// Вывод реквизита СогласованнаяСкидка на форму
	ПолеВводаСкидки = Элементы.Добавить("НИ_СогласованнаяСкидка", Тип("ПолеФормы"),Элементы.ГруппаСкидка);
	ПолеВводаСкидки.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВводаСкидки.ПутьКДанным = "Объект.НИ_СогласованнаяСкидка"; 
	ПолеВводаСкидки.УстановитьДействие("ПриИзменении","НИ_СогласованнаяСкидкаПриИзменении"); 
	
		
	//Добавление Команды ПересчитатьТаблицу
	Команда = Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "КомандаПересчитатьТаблицу";
	
	КнопкаФормы = Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Элементы.ГруппаСкидка);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
	// !!!
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПересчитатьТаблицу(Команда)
	// !!!
	// Неборак И.А.
	НИ_ПересчетТабличныхЧастей()
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Асинх Процедура НИ_СогласованнаяСкидкаПриИзменении(Элемент)
	// !!!
	// Неборак И.А.
	Если Не ЗначениеЗаполнено(Объект.Товары)И Не ЗначениеЗаполнено(Объект.Услуги) Тогда
		Возврат;
	КонецЕсли;
	
	Ответ = Ждать ВопросАсинх("Изменен процент скидки. Пересчитать таблицы товаров и услуг?", РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Да); 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		
		НИ_ПересчетТабличныхЧастей(); 
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура НИ_ПересчетТабличныхЧастей()  
	// !!!
	// Неборак И.А.
	Для Каждого Элемент Из Объект.Товары Цикл 
		
		РассчитатьСуммуСтроки(Элемент)
		
	КонецЦикла; 
	Для Каждого Элемент Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(Элемент)
		
	КонецЦикла; 
	
КонецПроцедуры


&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	// !!!
	// Неборак И.А.: Рассчет суммы с учетом скидки 
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество; 
	
	Если (Объект.НИ_СогласованнаяСкидка + ТекущиеДанные.Скидка) > 100 Тогда
		ТекущиеДанные.Сумма = 0; 
		ТекстСообщения = СтрШаблон("Скидка на позицию %1 превышает %2%%", ТекущиеДанные.Номенклатура, 100);
		Сообщить (ТекстСообщения);
	Иначе
		
		СуммаСкидки = ТекущиеДанные.Цена * ТекущиеДанные.Количество * (Объект.НИ_СогласованнаяСкидка + ТекущиеДанные.Скидка)/ 100; 
		СуммаСоСкидкой = ТекущиеДанные.Сумма - СуммаСкидки;
		ТекущиеДанные.Сумма = СуммаСоСкидкой;
		
	КонецЕсли; 
	
	РассчитатьСуммуДокумента();

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
